/**
 * Copy form build_kpt_project.jenkinsfile
 * @see https://github.com/xiaoyao9184/Kettle-Project-Toolbox/blob/master/jenkins/build_kpt_project.jenkinsfile
 */
pipeline {
    agent any
    parameters {
        string(name: 'ProjectName', defaultValue: 'default', description: 'project name!')
        
        //windows path
        string(name: 'PDIPath', defaultValue: 'D:\\Program\\Kettle\\data-integration', description: 'data-integration directory')
        string(name: 'KPTPath', defaultValue: 'D:\\Program\\Kettle\\Kettle-Project-Toolbox', description: 'Kettle-Project-Toolbox directory')
        string(name: 'ArchivePath', defaultValue: 'D:\\Archive', description: 'archive directory!')
        string(name: 'DeployPath', defaultValue: 'D:\\Program\\ETL', description: 'project directory!')
        
        //linux path
        // string(name: 'PDIPath', defaultValue: '/home/jenkins/kettle/data-integration', description: 'data-integration directory')
        // string(name: 'KPTPath', defaultValue: '/home/jenkins/kettle/kettle-project-toolbox', description: 'Kettle-Project-Toolbox directory')
        // string(name: 'ArchivePath', defaultValue: '/home/jenkins/archive', description: 'archive directory!')
        // string(name: 'DeployPath', defaultValue: '/home/jenkins/etl', description: 'project directory!')
        
        string(name: 'DeployProfile', defaultValue: 'dev', description: 'active profile!')
        string(name: 'PDIURL', defaultValue: 'https://jaist.dl.sourceforge.net/project/pentaho/Data%20Integration/7.1/pdi-ce-7.1.0.0-12.zip', description: 'The PDI URL!')
    }
    options {
        disableConcurrentBuilds()
        skipDefaultCheckout()
    }
    stages {
        
        // ready

        stage('Create info') {
            steps {
                echo 'Create info...'
                script {
                    flow = [
                        useSharedLibrary: false,
                        updateKPT: false,
                        downloadPDI: false,
                        applyPatch: false,
                        activeProfile: false,
                        addJenkinsJob: false
                    ]
                    archive = [
                        path: "${params.ArchivePath}",
                        file: null,
                        version: null
                    ]
                    info = [
                        workPath: null,
                        projectPath: null,
                        projectName: "${params.ProjectName}"
                    ]

                    try {
                        // on linux the following code may be reporting an error
                        library 'KPT_Shared_Libraries@master'
                        if(deploy){
                            println "Global variable 'deploy' exists use Shared Library"
                            flow."useSharedLibrary" = true
                        }
                    } catch(MissingPropertyException ex) {
                        println("Catching the MissingPropertyException, miss 'deploy' mean not use Shared Library!");
                    } catch(Exception ex) {
                        println("Catching unknow Exception, that mean not use Shared Library!");
                    }
                    
                    archive."path" = "${archive.path}"
                    info."PDIPath" = params.PDIPath
                    info."KPTPath" = params.KPTPath
                    info."DeployPath" = params.DeployPath
                }
            }
        }

        stage('Check PDI and KPT') {
            steps {
                echo 'Check kettle tool...'
                script {
                    if(flow.useSharedLibrary){
                        //update KPT when not exist or KPT path is manage by git
                        flow."updateKPT" = deploy.check_path_not_exists_or_manage_by_git("${info.KPTPath}")

                        //download PDI with not exist
                        flow."downloadPDI" = !deploy.check_file_exists("${info.PDIPath}")
                    }else if(isUnix()){
                        //update KPT when not exist or KPT path is manage by git
                        flow."updateKPT" = sh(
                                script: """
                                [ -d "${info.KPTPath}" ] && [ ! -d "${info.KPTPath}/.git" ] && echo "false" || echo "true"
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                        //download PDI with not exist
                        flow."downloadPDI" = sh(
                                script: """
                                [ -d "${info.PDIPath}" ] && echo "false" || echo "true"
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                    }else{
                        //update KPT when not exist or KPT path is manage by git
                        flow."updateKPT" = bat(
                                script: """
                                @echo off
                                if exist "${info.KPTPath}" (
                                    if exist "${info.KPTPath}\\.git" (
                                        echo true
                                    ) else (
                                        echo false
                                    )
                                ) else (
                                    echo true
                                )
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                        //download PDI with not exist
                        flow."downloadPDI" = bat(
                                script: """
                                @echo off
                                if exist "${info.PDIPath}" (
                                    echo false
                                ) else (
                                    echo true
                                )
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                    }
                }
            }
        }

        stage('Update KPT') {
            when { 
                expression { flow.updateKPT }
            }
            steps {
                dir("${info.KPTPath}") {
                    echo 'Update KPT...'
                    git branch: 'master',
                        url: 'https://github.com/xiaoyao9184/Kettle-Project-Toolbox.git'
                }
            }
        }

        stage('Download PDI') {
            when { 
                expression { flow.downloadPDI }
            }
            steps {
                echo 'Download PDI...'
                fileOperations([
                    fileDownloadOperation(
                        url: "${params.PDIURL}",
                        targetFileName: 'pdi-ce.zip',
                        targetLocation: "${WORKSPACE}",
                        userName: '',
                        password: ''),
                    fileUnZipOperation(
                        filePath: 'pdi-ce.zip',
                        targetLocation: "."),
                    folderCopyOperation(
                        destinationFolderPath: "${params.PDIPath}",
                        sourceFolderPath: './data-integration'),
                    folderDeleteOperation(
                        './data-integration'),
                    folderDeleteOperation(
                        './pdi-ce.zip')
                ])
            }
        }

        // build

        stage('[Build]Checkout') {
            steps {
                dir("${params.ProjectName}") {
                    echo 'Checkout project...'
                    checkout scm
                }
            }
        }

        stage('[Build]Link') {
            when { 
                expression { info.projectName }
            }
            steps {
                echo 'Link KPT...'
                script {
                    if(isUnix()){
                        sh """
                        bash "${info.KPTPath}/tool/LINK_KPT.sh" "\$(pwd)" "${info.PDIPath}" <&-
                        """
                    }else{
                        bat """
                        call "${info.KPTPath}\\tool\\LINK_KPT.bat" "%cd%" "${info.PDIPath}"
                        """
                    }
                }
            }
        }

        stage('[Build]Package') {
            when { 
                expression { info.projectName }
            }
            steps {
                echo 'Package project...'
                script {
                    if(isUnix()){
                        sh """
                        bash "\$(pwd)/tool/ZIP_DEPLOY_PATH.sh" "\$(pwd)/${info.projectName}" <&-
                        """
                    }else{
                        bat """
                        call %cd%\\tool\\ZIP_DEPLOY_PATH.bat %cd%\\${info.projectName}
                        """
                    }
                }
            }
        }

        stage('[Build]Archive') {
            when { 
                expression { info.projectName }
            }
            steps {
                echo "Archive project..."
                script {
                    if(isUnix()){
                        archive."path" = "${archive.path}/${info.projectName}"
                        archive."file" = sh(
                            script: "find . -name '\\[Deploy\\]${info.projectName}*.zip' -print", 
                            returnStdout: true).trim()
                        archive."version" = sh(
                            script: "basename \$(find . -name '\\[Deploy\\]${info.projectName}*.zip' -print) .zip", 
                            returnStdout: true).trim()
                    }else{
                        archive."path" = "${archive.path}\\${info.projectName}"
                        archive."file" = bat(
                            script: "@echo off & FOR %%F IN ([Deploy]${info.projectName}*.zip) DO echo %%~nxF", 
                            returnStdout: true).trim()
                        archive."version" = bat(
                            script: "@echo off & FOR %%F IN ([Deploy]${info.projectName}*.zip) DO echo %%~nF", 
                            returnStdout: true).trim()
                    }

                    if (!archive.file) {
                        echo "No archive file!"
                        archive = null
                        currentBuild.result = 'FAILURE'
                    }
                }
                echo "Archive path is: ${archive.path}"
                echo "Archive file is: ${archive.file}"
                echo "Archive version is: ${archive.version}"
                
                script {
                    if(isUnix()){
                        sh """
                        [ -d "${archive.path}" ] || mkdir -p "${archive.path}"
                        mv "${archive.file}" "${archive.path}/"
                        """
                    }else{
                        bat """
                        @echo off
                        IF NOT EXIST "${archive.path}" MKDIR "${archive.path}"
                        MOVE /y "${archive.file}" "${archive.path}"
                        """
                    }
                }
            }
        }
        
        // deploy

        stage('[Deploy]Check archive info') {
            steps {
                echo "Check archive..."
                script {
                    if (!info.projectName) {
                        echo "No 'ProjectName' param!"
                        archive = null
                        currentBuild.result = 'UNSTABLE'
                    } else if (!archive) {
                        echo "No archive info!"
                        archive = null
                        currentBuild.result = 'FAILURE'
                    }
                }
            }
        }
        
        stage('[Deploy]Create workspace info') {
            when { 
                expression { archive }
            }
            steps {
                echo 'Create workspace info...'
                script {
                    if(isUnix()){
                        info."workPath" = "${info.DeployPath}/${archive.version}"
                        info."projectPath" = "${info.workPath}/${info.projectName}"
                    }else{
                        info."workPath" = "${info.DeployPath}\\${archive.version}"
                        info."projectPath" = "${info.workPath}\\${info.projectName}"
                    }

                    def projectPathExists = false
                    if(flow.useSharedLibrary){
                        projectPathExists = deploy.check_file_exists("${info.projectPath}")
                    }else if(isUnix()){
                        projectPathExists = sh(
                                script: """
                                [ -d "${info.projectPath}" ] && echo "true" || echo "false"
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                    }else{
                        projectPathExists = bat(
                                script: """
                                @echo off
                                if exist "${info.projectPath}" (
                                    echo true
                                ) else (
                                    echo false
                                )
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                    }
                    if (projectPathExists) {
                        println "Deploy target path already exists ${info.projectPath}, skip it!"
                        archive = null
                    } else {
                        println "Deploy project path is: ${info.projectPath}"
                    }
                }
            }
        }

        stage('[Deploy]Release archive to project path') {
            when { 
                expression { archive } 
            }
            steps {
                dir("${info.projectPath}") {
                    echo 'Release archive...'
                    checkout scm
                }
            }
        }

        stage('[Deploy]Check project') {
            when { 
                expression { archive }
            }
            steps {
                echo 'Check project path...'
                script {
                    def profileFileExists = false
                    if(flow.useSharedLibrary){
                        profileFileExists = deploy.check_file_exists("${info.projectPath}/.profile/.profile")
                    }else if(isUnix()){
                        profileFileExists = sh(
                                script: """
                                [ -f "${info.projectPath}/.profile/.profile" ] && echo "true" || echo "false"
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                    }else{
                        profileFileExists = bat(
                                script: """
                                @echo off
                                if exist "${info.projectPath}\\.profile\\.profile" (
                                    echo true
                                ) else (
                                    echo false
                                )
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                    }
                    if (profileFileExists && params.DeployProfile) {
                        flow."activeProfile" = true
                        println 'Profile needs to be activated!'
                    }

                    def patchFileExists = false
                    if(flow.useSharedLibrary && isUnix()){
                        patchFileExists = 
                            deploy.check_file_exists("${info.projectPath}/patch.PatchPDI.sh")
                    }else if(flow.useSharedLibrary && !isUnix()){
                        patchFileExists = 
                            deploy.check_file_exists("${info.projectPath}/patch.PatchPDI.bat")
                    }else if(isUnix()){
                        patchFileExists = sh(
                                script: """
                                [ -f "${info.projectPath}/patch.PatchPDI.sh" ] && echo "true" || echo "false"
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                    }else{
                        patchFileExists = bat(
                                script: """
                                @echo off
                                if exist "${info.projectPath}\\patch.PatchPDI.bat" (
                                    echo true
                                ) else (
                                    echo false
                                )
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                    }
                    if (patchFileExists) {
                        flow."applyPatch" = true
                        println 'Patch needs to be applied!'
                    }

                    def jenkinsPathExists = false
                    if(flow.useSharedLibrary){
                        jenkinsPathExists = deploy.check_file_exists("${info.projectPath}/jenkins") 
                    }else if(isUnix()){
                        jenkinsPathExists = sh(
                                script: """
                                [ -d "${info.projectPath}/jenkins" ] && echo "true" || echo "false"
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                    }else{
                        jenkinsPathExists = bat(
                                script: """
                                @echo off
                                if exist "${info.projectPath}\\jenkins" (
                                    echo true
                                ) else (
                                    echo false
                                )
                                """, 
                                returnStdout: true)
                            .trim()
                            .toBoolean()
                    }
                    if (jenkinsPathExists) {
                        flow."addJenkinsJob" = true
                        println 'JenkinsJob needs to be added!'
                    }
                }
            }
        }

        stage('[Deploy]Link PDI and KPT[Unix]') {
            when { 
                expression { archive } 
            }
            steps {
                echo 'Link PDI and KPT...'
                script {
                    if(isUnix()){
                        sh "bash ${params.KPTPath}/tool/LINK_KPT.sh ${info.workPath} ${info.PDIPath} <&-" 
                    }else{
                        bat "call ${params.KPTPath}\\tool\\LINK_KPT.bat ${info.workPath} ${info.PDIPath}"
                    }
                }
            }
        }

        stage('[Deploy]Apply PDI patch') {
            when { 
                expression { archive && flow.applyPatch } 
            }
            steps {
                dir("${info.projectPath}") {
                    script {
                        if(isUnix()){
                            sh 'bash patch.PatchPDI.sh <&-' 
                        }else{
                            bat 'call patch.PatchPDI.bat'
                        }
                    }
                }
            }
        }
        
        stage('[Deploy]Active project profile') {
            when { 
                expression { archive && flow.activeProfile } 
            }
            steps {
                echo 'Active project profile...'
                script {
                    dir("${info.projectPath}") {
                        if(flow.useSharedLibrary){
                            deploy.rename_file(
                                "${info.projectPath}/.profile/.profile",
                                "${info.projectPath}/.profile/${params.DeployProfile}.profile")
                        }else if(isUnix()){
                            sh """
                            mv "./.profile/.profile" "./.profile/${params.DeployProfile}.profile"
                            """
                        }else{
                            bat """
                            @echo off
                            cd ".profile"
                            REN ".profile" "${params.DeployProfile}.profile"
                            """
                        }
                    }
                }
            }
        }

        stage('[Deploy]Add Jenkins scheduling job') {
            when { 
                expression { archive && flow.addJenkinsJob } 
            }
            steps {
                dir("${info.projectPath}") {
                    echo 'Add Jenkins scheduling job...'
                    jobDsl(
                        targets: "jenkins/deploy_kpt_project_jenkinsfile.groovy", 
                        removedJobAction: 'DELETE', 
                        removedViewAction: 'DELETE',
                        additionalParameters: [
                            ProjectPath: "${info.projectPath}"
                        ],
                        lookupStrategy: 'SEED_JOB',
                        sandbox: false
                    )
                }
            }
        }
    }
    post {
        cleanup {
            deleteDir()
        }
    }
}
